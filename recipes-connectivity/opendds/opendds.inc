SUMMARY = "OpenDDS is an open source C++ implementation of the Object Management Group (OMG) Data Distribution Service (DDS)"
HOMEPAGE = "https://opendds.org"

LICENSE = "OpenDDS"
LIC_FILES_CHKSUM = "file://LICENSE;md5=11ee76f6fb51f69658b5bb8327c50b11"

inherit autotools

SRC_URI += "\
    ${@bb.utils.contains('PACKAGECONFIG', 'ishapes', 'file://0001-adding-the-ishapes-demo.patch', '', d)} \
    file://0002-fixing-DoAll-testing-DoAll.patch \
"

PACKAGECONFIG ??= "tests"
PACKAGECONFIG:append = " ${@bb.utils.filter('DISTRO_FEATURES', 'ipv6', d)}"

PACKAGECONFIG[ishapes] = "--qt --qt-include=${STAGING_INCDIR},,qtbase"
PACKAGECONFIG[ipv6] = "--ipv6,--no-ipv6,"
PACKAGECONFIG[tests] = "--tests --gtest=${STAGING_INCDIR}/..,--no-tests,googletest cmake,perl perl-module-env perl-module-lib perl-module-english perl-module-file-glob perl-module-file-copy perl-module-sys-hostname perl-module-file-spec-functions perl-module-findbin perl-module-cwd perl-module-filehandle perl-module-posix perl-module-file-temp perl-module-file-spec perl-module-scalar-util perl-module-findbin perl-module-getopt-long"

DEPENDS += "\
    cmake-native \
    opendds-native \
    openssl \
    perl-native \
    xerces-c \
"

RDEPENDS:${PN}-dev += "\
    bash \
    coreutils \
    make \
    perl \
"

S = "${WORKDIR}/git"

# Set the build directory to be the source directory
B = "${S}"

do_unpack_extra() {
    # the configure script does not support arguments to the cross compiler
    # but the bitbake toolchain needs those
    # create shell scripts which inject the arguments into the calls
    cc_bin=${S}/${HOST_PREFIX}gcc
    echo '#!/bin/sh' > ${cc_bin}
    echo "${CC} \"\$@\"" >> ${cc_bin}
    chmod +x ${cc_bin}

    cxx_bin=${S}/${HOST_PREFIX}g++
    echo '#!/bin/sh' > ${cxx_bin}
    echo "${CXX} \"\$@\"" >> ${cxx_bin}
    chmod +x ${cxx_bin}

    ar_bin=${S}/${HOST_PREFIX}ar
    echo '#!/bin/sh' > ${ar_bin}
    echo "${AR} \"\$@\"" >> ${ar_bin}
    chmod +x ${ar_bin}
}
addtask unpack_extra after do_unpack before do_patch

OECONF ??= ""
OECONF:append = "\
    --prefix=${prefix} \
    --verbose \
    --no-tests \
    --no-rapidjson \
    --security \
    --openssl=${WORKDIR}/recipe-sysroot/usr \
    --xerces3=${WORKDIR}/recipe-sysroot/usr \
    ${ACE_TAO_OPTION} \
"

OECONF:append:class-target = "\
    --host-tools=${STAGING_BINDIR_NATIVE}/DDS_HOST_ROOT \
    --target=linux-cross \
    --target-compiler=${S}/${HOST_PREFIX}g++ \
    ${PACKAGECONFIG_CONFARGS} \
"
OECONF:append:class-native = "\
    --target=linux \
    --host-tools-only \
    --security \
    --openssl=${WORKDIR}/recipe-sysroot-native/usr \
    --xerces3=${WORKDIR}/recipe-sysroot-native/usr \
"
OECONF:append:class-nativesdk = "\
    --compiler=${S}/${HOST_PREFIX}g++ \
    --target=linux \
    --host-tools-only \
"

do_configure() {
    ./configure ${OECONF}
}

SOLIBS = ".so"
FILES_SOLIBSDEV = ""

do_install:append:class-target() {
    rm ${D}${datadir}/dds/dds/Version.h
    cp ${D}${includedir}/dds/Version.h ${D}${datadir}/dds/dds

    sed -i -e s:${D}/::g ${D}${datadir}/dds/dds-devel.sh

    # workaround: /usr/share/dds/dds/idl/IDLTemplate.txt should be placed into target sysroot
    install -d ${D}${datadir}/dds/dds/idl
    cp ${S}/dds/idl/IDLTemplate.txt ${D}${datadir}/dds/dds/idl

    install -d ${D}${libdir}
    #install -m 0755 ${S}/*.so ${D}${libdir} 
    for shared_lib in ${D}${libdir}/*.so.*; do
        if [ -f $shared_lib ]; then
            baselib=$(basename $shared_lib)
            shortlib=$(echo $baselib | sed 's/.so.*//')
            extn=$(echo $baselib | sed -n 's/^[^.]*\.so//p')
            extn=$(echo $extn | sed 's/[^. 0-9]*//g')
            while [ -n "$extn" ]; do
                extn=$(echo $extn | sed 's/\.[^.]*$//')
                ln -sf $baselib ${D}${libdir}/$shortlib.so$extn
            done
        fi
    done

    # Copy OpenDDS source tree folders to sysroot-destdir/usr/share/DDS_ROOT
    # to support building OpenDDS apps in their own layers
    . ${S}/setenv.sh
    mkdir -p ${D}${datadir}/DDS_ROOT/ACE_wrappers
    cp -r ${DDS_ROOT}/dds ${D}${datadir}/DDS_ROOT
    cp -r ${DDS_ROOT}/MPC ${D}${datadir}/DDS_ROOT
    cp -r ${DDS_ROOT}/tools ${D}${datadir}/DDS_ROOT
    cp -r ${DDS_ROOT}/cmake ${D}${datadir}/DDS_ROOT
    cp -r ${DDS_ROOT}/lib ${D}${datadir}/DDS_ROOT
    cp -r ${DDS_ROOT}/performance-tests ${D}${datadir}/DDS_ROOT
    cp ${DDS_ROOT}/*.GNU ${D}${datadir}/DDS_ROOT
    cp ${DDS_ROOT}/*.txt ${D}${datadir}/DDS_ROOT
    cp -r ${DDS_ROOT}/ACE_wrappers/ace ${D}${datadir}/DDS_ROOT/ACE_wrappers
    cp -r ${DDS_ROOT}/ACE_wrappers/bin ${D}${datadir}/DDS_ROOT/ACE_wrappers
    cp -r ${DDS_ROOT}/ACE_wrappers/include ${D}${datadir}/DDS_ROOT/ACE_wrappers
    cp -r ${DDS_ROOT}/ACE_wrappers/MPC ${D}${datadir}/DDS_ROOT/ACE_wrappers
    cp -r ${DDS_ROOT}/ACE_wrappers/TAO ${D}${datadir}/DDS_ROOT/ACE_wrappers
    cp -r ${DDS_ROOT}/ACE_wrappers/lib ${D}${datadir}/DDS_ROOT/ACE_wrappers
    cp -r ${DDS_ROOT}/ACE_wrappers/ACEXML ${D}${datadir}/DDS_ROOT/ACE_wrappers
    cp ${DDS_ROOT}/ACE_wrappers/*.txt ${D}${datadir}/DDS_ROOT/ACE_wrappers

    if [ "${@bb.utils.contains("PACKAGECONFIG", "ishapes", "1", "0", d)}" = "1" ]; then
        install -d ${D}${bindir}
        install -m 0755 ${S}/examples/DCPS/ishapes/ishapes ${D}${bindir}/
    fi
    if [ "${@bb.utils.contains("PACKAGECONFIG", "tests", "1", "0", d)}" = "1" ]; then
        install -d ${D}${bindir}
        install -m 0755 ${S}/tests/auto_run_tests.pl ${D}${bindir}/
        install -d ${D}${datadir}/DDS_ROOT/bin/
        install -d ${D}${datadir}/DDS_ROOT/bin/PerlDDS
        install -m 0755 ${S}/bin/PerlDDS/* ${D}${datadir}/DDS_ROOT/bin/PerlDDS/
        install -d ${D}${datadir}/DDS_ROOT/tests
        cp -r  ${S}/tests ${D}${datadir}/DDS_ROOT
    fi
}

do_install:append:class-native() {
    rm ${D}${datadir}/dds/bin/opendds_idl
    rm ${D}${datadir}/ace/bin/ace_gperf
    rm ${D}${datadir}/ace/bin/tao_idl
}

do_install:append:class-native() {
    # Prepare HOST_ROOT expected by DDS for target build
    mkdir -p ${D}${bindir}/DDS_HOST_ROOT/ACE_wrappers/bin
    mkdir -p ${D}${bindir}/DDS_HOST_ROOT/bin
    ln -sr ${D}${bindir}/opendds_idl ${D}${bindir}/DDS_HOST_ROOT/bin/opendds_idl
    ln -sr ${D}${bindir}/ace_gperf ${D}${bindir}/DDS_HOST_ROOT/ACE_wrappers/bin/ace_gperf
    ln -sr ${D}${bindir}/tao_idl ${D}${bindir}/DDS_HOST_ROOT/ACE_wrappers/bin/tao_idl
}

do_install:append:class-nativesdk() {
    ln -sf ${bindir}/opendds_idl ${D}${datadir}/dds/bin/opendds_idl
    ln -sf ${bindir}/ace_gperf ${D}${datadir}/ace/bin/ace_gperf
    ln -sf ${bindir}/tao_idl ${D}${datadir}/ace/bin/tao_idl
}

INSANE_SKIP:${PN} += "dev-so"
INSANE_SKIP:${PN} += "libdir"

PACKAGES += "${PN}-ishapes"

FILES:${PN} += "${libdir}/*.so*"
FILES:${PN}-dev += "${datadir}"
FILES:${PN}-ishapes += "${bindir}/ishapes"

ALLOW_EMPTY:${PN}-ishapes = "1"

BBCLASSEXTEND = "native nativesdk"
